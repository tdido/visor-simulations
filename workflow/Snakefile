
configfile: "config/config.yaml"


STAGE_LIST = config['stage_list']

GENOME_URL = config['genome_url']

GENOME_FILE = config['genome_file']


def get_resource(rule, resource):
    try:
        return config["resources"][rule][resource]
    except KeyError:
        return config["resources"]["default"][resource]

rule all:
    input:
        [f"results/{stage}_sniffles" for stage in STAGE_LIST]


rule visor_hack:
    input:
        fasta = f"resources/genome/{GENOME_FILE}",
        bed1 = f"resources/{{stage}}/HACk.h1.bed",
        bed2 = f"resources/{{stage}}/HACk.h2.bed"
    output:
        h1_fa = f"results/{{stage}}_HACk/h1.fa",
        h1_fa_fai = f"results/{{stage}}_HACk/h1.fa.fai",
        h2_fa = f"results/{{stage}}_HACk/h2.fa",
        h2_fa_fai = f"results/{{stage}}_HACk/h2.fa.fai",
        folder = directory(f"results/{{stage}}_HACk")
    log:
        f"results/log/visor_hack/{{stage}}.log"
    benchmark:
        f"results/log/visor_hack/{{stage}}.bmk"
    threads: get_resource("visor_hack", "threads")
    resources:
        mem_mb=get_resource("visor_hack", "mem_mb"),
        runtime=get_resource("visor_hack", "runtime"),
        slurm_partition=get_resource("visor_hack", "partition")
    conda:
        "envs/visor.yaml"
    shell:"""
        VISOR HACk -b {input.bed1} {input.bed2} -g {input.fasta} \
        -o {output.folder} > {log} 2>&1
    """


rule generate_bed:
    input:
        rules.visor_hack.output.h1_fa,
        rules.visor_hack.output.h2_fa,
        rules.visor_hack.output.h1_fa_fai,
        rules.visor_hack.output.h2_fa_fai,
    output:
        f"results/{{stage}}_HACk/laser.simple.bed"
    log:
        f"results/log/generate_bed/{{stage}}.log"
    benchmark:
        f"results/log/generate_bed/{{stage}}.bmk"
    threads: get_resource("generate_bed", "threads")
    resources:
        mem_mb=get_resource("generate_bed", "mem_mb"),
        runtime=get_resource("generate_bed", "runtime"),
        slurm_partition=get_resource("generate_bed", "partition")
    shell:"""
        sh workflow/scripts/makeBED.sh {wildcards.stage} {GENOME_FILE} > {log} 2>&1
    """   


rule visor_laser:
    input:
        fasta = f"resources/genome/{GENOME_FILE}",
        hack_output = f"results/{{stage}}_HACk",
        bed = f"results/{{stage}}_HACk/laser.simple.bed"
    output:
        bam = f"results/{{stage}}_LASeR/sim.srt.bam",
        folder = directory(f"results/{{stage}}_LASeR")
    log:
        f"results/log/visor_laser/{{stage}}.log"
    benchmark:
        f"results/log/visor_laser/{{stage}}.bmk"
    threads: get_resource("visor_laser", "threads")
    resources:
        mem_mb=get_resource("visor_laser", "mem_mb"),
        runtime=get_resource("visor_laser", "runtime"),
        slurm_partition=get_resource("visor_laser", "partition")
    conda:
        "envs/visor.yaml"
    params:
        coverage=config['params']['coverage'],
        read_type=config['params']['read_type']
    shell:"""
        VISOR LASeR -g {input.fasta} -s {input.hack_output} -b {input.bed} \
        -o {output.folder} --threads {threads} --coverage {params.coverage} \
        --fastq --read_type {params.read_type} > {log} 2>&1
    """


rule sniffles:
    input:
        bam = f"results/{{stage}}_LASeR/sim.srt.bam"
    output:
        directory(f"results/{{stage}}_sniffles"),
        vcf = f"results/{{stage}}_sniffles/{{stage}}.vcf"
    log:
        f"results/log/sniffles/{{stage}}.log"
    benchmark:
        f"results/log/sniffles/{{stage}}.bmk"
    threads: get_resource("sniffles", "threads")
    resources:
        mem_mb=get_resource("sniffles", "mem_mb"),
        runtime=get_resource("sniffles", "runtime"),
        slurm_partition=get_resource("sniffles", "partition")
    conda:
        "envs/sniffles.yaml"
    shell:"""
        sniffles -i {input.bam} -v {output.vcf} > {log} 2>&1
    """
    